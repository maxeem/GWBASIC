1 REM For Tandy 1000
2 REM NOTE: Will NOT run on IBM without
3 REM following modifications:
4 REM 1. Change SCREEN 5 in line 140 to SCREEN 1
5 REM 2. Change line 190 to: IF C>3 then C=1
6 REM 3. Change both INT(RND*16) statements in line 200 to INT(RND*4)
10 CLEAR ,,,32768!
20 CLS
30 ON KEY(10) GOSUB 230:KEY (10) ON
40 PALETTE
50 'ABSTRACT - PROVES THAT YOU DON'T NEED TO KNOW FANCY MATH FOR NEAT GRAPHICS
60 'BY ERIC ASBERRY FEBRUARY 1986
70 RANDOMIZE TIMER
80 DEFINT A-Z
90 PRINT" Press [F10] to exit ":PRINT
100 INPUT"Background flash (Y/N)";YN1$
110 IF YN1$="y" THEN YN1$="Y"
120 C=1
130 KEY OFF
140 CLS:SCREEN 5:X1=80:X2=240:Y1=100:Y2=100
150 A$="ABSTRACT - BY ERIC ASBERRY":LOCATE 25,(20-(LEN(A$)/2)),0:PRINT A$;
160 GOSUB 220
170 X1=X1+XD1:X2=X2+XD2:Y1=Y1+YD1:Y2=Y2+YD2
180 IF (X1>320 OR X1<0) OR (X2>320 OR X2<0) OR (Y2<0 OR Y2>189) OR (Y1<0 OR Y1>189) THEN X1=X1-XD1:X2=X2-XD2:Y1=Y1-YD1:Y2=Y2-YD2:GOSUB 220:C=C+1:GOTO 170
190 IF C>16 THEN C=1
200 IF YN1$="Y" THEN C1=INT(RND*16):C2=INT(RND*16):PALETTE C1,C2
210 LINE(X1,Y1)-(X2,Y2),C:GOTO 170
220 XD1=INT(RND*10)-5:XD2=INT(RND*10)-5:YD1=INT(RND*10)-5:YD2=INT(RND*10)-5:RETURN
230 SCREEN 0,0,0
