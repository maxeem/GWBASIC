100 '
110 'Summary of System Configuration
120 '
125 COMMON RH: IF RH=0 THEN SYSTEM ELSE CLEAR
130 SCREEN 0: KEY OFF: WIDTH 40: CLS: LOCATE 6,1,0
140 DEF SEG=0: PRINT"Your Tandy 1000 has";
150 PRINT PEEK(1043)*2^10 + PEEK(1044)*2^18;
160 PRINT "bytes of":PRINT "total memory,";
170 PRINT (PEEK(1040) AND 1)*(1 + INT(PEEK(1040)/64));
180 PRINT "floppy disk drive(s),"
190 A = PEEK(1040) AND 48
200 IF A=0 THEN PRINT "no";
210 IF A=16 THEN PRINT "a 40 x 25 graphics";
220 IF A=32 THEN PRINT "an 80 x 25 graphics";
230 IF A=48 THEN PRINT"a monochrome";
240 PRINT " display monitor,":PRINT "and does ";
250 IF (PEEK(1041) AND 14) = 0 THEN PRINT "not ";
260 PRINT "have an RS-232 card(s)."
270 PRINT "The CAPS-LOCK LED is currently ";
280 A = PEEK(1048) AND 64
290 IF A=0 THEN PRINT "OFF," ELSE PRINT "ON,"
300 PRINT "and the NUM-LOCK LED is ";
310 A = PEEK(1048) AND 32
320 IF A=0 THEN PRINT "OFF." ELSE PRINT "ON."
330 PRINT: PRINT "Press <Any Key> To Continue...";
340 POKE 1050, PEEK(1052): A$=INPUT$(1)
350 '
360 'Demonstrate Sensing of SHIFT/CTRL/ALT
370 '
380 CLS: LOCATE 4,1
390 PRINT "Press either SHIFT key, CTRL, and/or"
400 PRINT "the ALT key.  Press ENTER to continue."
410 LOCATE 8,8: PRINT "Right Shift: ";
420 A = PEEK(1047) AND 1
430 IF A=1 THEN PRINT"On " ELSE PRINT"Off"
440 LOCATE 9,9: PRINT "Left Shift: ";
450 A = PEEK(1047) AND 2
460 IF A=2 THEN PRINT "On " ELSE PRINT "Off"
470 LOCATE 10,11: PRINT "CTRL Key: ";
480 A = PEEK(1047) AND 4
490 IF A=4 THEN PRINT "On " ELSE PRINT "Off"
500 LOCATE 11,12: PRINT "ALT Key: ";
510 A = PEEK(1047) AND 8
520 IF A=8 THEN PRINT "On " ELSE PRINT "Off"
530 A$ = INKEY$: IF A$<>CHR$(13) THEN 410
540 '
550 'Disable and Re-enable CTRL-BREAK
560 '
570 CLS: S1 = PEEK(108): S2 = PEEK(109)
580 S3 = PEEK(110): S4 = PEEK(111)
590 FOR A=108 TO 111: POKE A, PEEK(A+4): NEXT A
600 LOCATE 4,1: PRINT "Try to stop this loop ";
610 PRINT "by pressing CTRL-BREAK ... ": PRINT
620 FOR A=1 TO 100: PRINT A;
630 FOR B=1 TO 100: NEXT B,A
640 LOCATE 18,1: PRINT "Couldn't do it, could you?"
650 POKE 108,S1: POKE 109,S2: POKE 110,S3
660 POKE 111,S4: PRINT: PRINT "Press <ANY KEY> to return"
670 PRINT "to the main menu program ...": DEF SEG=0
680 POKE 1050, PEEK(1052): I$=INPUT$(1): WIDTH 80
690 LOCATE 12,28: PRINT "One moment please": RUN "menu"
