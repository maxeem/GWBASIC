70 COMMON RH: IF RH=0 THEN SYSTEM
80 CLEAR: ON ERROR GOTO 950
90 KEY (17) ON: KEY 17, CHR$(0)+CHR$(1): ON KEY (17) GOSUB 910
100 '
110 'Sample Three-Line Message
120 '
130 SCREEN 0:KEY OFF:WIDTH 80:COLOR 7,0,0
140 CLS:WORD$="VERY":LN=1:GOSUB 770
150 WORD$="BIG":LN=2:GOSUB 770
160 WORD$="LETTERS":LN=3:GOSUB 770
170 GOSUB 690:CLS
180 '
190 'Now Add Some Color
200 '
210 COLOR 1:WORD$="Now in":LN=1:GOSUB 770
220 COLOR 4:WORD$="living":LN=2:GOSUB 770
230 COLOR 2:WORD$="COLOR!":LN=3:GOSUB 770
240 GOSUB 690:CLS
250 '
260 'On the Margins
270 '
280 COLOR 3:WORD$=CHR$(17)+"Left     ":LN=1:GOSUB 770
290 COLOR 5:WORD$="    Right"+CHR$(16):LN=2:GOSUB 770
300 COLOR 9:WORD$="Center":LN=3:GOSUB 770
310 GOSUB 690:CLS
320 '
330 'Unique Characters
340 '
350 COLOR 10:WORD$="CHR$(7)="+CHR$(7):LN=1:GOSUB 770
360 COLOR 11:WORD$="8="+CHR$(8)+" 9="+CHR$(9)
370 LN=2:GOSUB 770:COLOR 12
380 WORD$="10="+CHR$(10)+" 11="+CHR$(11)
390 LN=3:GOSUB 770:GOSUB 690:CLS
400 '
410 'More Unique Characters
420 '
430 COLOR 13:WORD$="12="+CHR$(12)+" 13="+CHR$(13)
440 LN=1:GOSUB 770:COLOR 14
450 WORD$="28="+CHR$(28)+" 29="+CHR$(29)
460 LN=2:GOSUB 770:COLOR 15
470 WORD$="30="+CHR$(30)+" 31="+CHR$(31)
480 LN=3:GOSUB 770:GOSUB 690:CLS
490 '
500 'Flashy Final Demonstration
510 '
520 COLOR 14,1,1:CLS
530 WORD$="ONE":LN=1:GOSUB 770
540 WORD$="THOUSAND":LN=2:GOSUB 770
550 COLOR 10:LOCATE 4,8:PRINT"Vol. 1, No. 10";
560 LOCATE 4,60:PRINT"November, 1986";
570 COLOR 15:LOCATE 18,24
580 PRINT CHR$(201);STRING$(33,205);CHR$(187)
590 LOCATE 19,24:PRINT CHR$(186);
600 PRINT"  The User Supported Newsletter  ";CHR$(186)
610 LOCATE 20,24:PRINT CHR$(186);" Especially for";
620 PRINT" Your Tandy 1000! ";CHR$(186)
630 LOCATE 21,24
640 PRINT CHR$(200);STRING$(33,205);CHR$(188)
650 GOSUB 690:COLOR 7,0,0:GOTO 910
660 '
670 'Pause Subroutine
680 '
690 COLOR 0,7:LOCATE 25,27:PRINT " Press ";
700 COLOR 16,7:PRINT"<ANY KEY>";
710 COLOR 0,7:PRINT" to continue ";:COLOR 7,0
720 A$=INKEY$:IF A$<>"" THEN 720
730 A$=INPUT$(1):RETURN
740 '
750 'Display Message in WORD$
760 '
770 DEF SEG=65446!
780 IF LEN(WORD$)>10 THEN WORD$=LEFT$(WORD$,10)
790 LN=LN AND 3:IF LN=0 THEN LN=1
800 FOR X=0 TO 7
810 LOCATE LN*8+X-7,41-LEN(WORD$)*4,0
820 FOR Y=1 TO LEN(WORD$)
830 Q=PEEK(ASC(MID$(WORD$,Y,1))*8+X+14)
835 C(X)=Q
840 IF Q=0 THEN PRINT STRING$(8,32);:GOTO 870
850 FOR Z=7 TO 0 STEP -1
860 PRINT CHR$(32+187*SGN(Q AND 2^Z));:NEXT Z
870 NEXT Y,X:RETURN
880 '
890 'Return to Main Menu
900 '
910 CLS: LOCATE 12,28: PRINT "One moment please...": RUN "menu"
920 '
930 'Error Trap
940 '
950 RESUME NEXT
