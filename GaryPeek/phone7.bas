100 REM MAKE WORDS FROM PHONE NUMBERS
110 GOSUB 400
120 DIM PHONE(20)
130 CLS:PRINT "ENTER 7 DIGIT PHONE NUMBER  ";
140 INPUT PHONE$
150 FOR DIGIT=1 TO LEN(PHONE$)
160   PHONE(DIGIT)=VAL(MID$(PHONE$,DIGIT,1))
170 NEXT DIGIT
180 FOR A=1 TO 3
185 A$=L$(PHONE(1),A)
190 FOR B=1 TO 3
195 B$=L$(PHONE(2),B)
200 FOR C=1 TO 3
205 C$=L$(PHONE(3),C)
210 FOR D=1 TO 3
215 D$=L$(PHONE(4),D)
220 FOR E=1 TO 3
225 E$=L$(PHONE(5),E)
230 FOR F=1 TO 3
235 F$=L$(PHONE(6),F)
240 FOR G=1 TO 3
245 G$=L$(PHONE(7),G)
270 PRINT A$+B$+C$+D$+E$+F$+G$;"   ";
280 NEXT G
285 PRINT
290 NEXT F
300 NEXT E
310 NEXT D
320 NEXT C
330 NEXT B
340 NEXT A
370 IN$=INKEY$:IF IN$="" THEN 370
380 IF IN$=CHR$(27) THEN END
390 GOTO 130
400 DIM L$(9,4)
410 FOR A=0 TO 9
420 FOR B=1 TO 3
430 READ L$(A,B)
440 NEXT B
450 NEXT A
460 RETURN
470 DATA "?","?","?"
480 DATA "?","?","?"
490 DATA "A","B","C"
500 DATA "D","E","F"
510 DATA "G","H","I"
520 DATA "J","K","L"
530 DATA "M","N","O"
540 DATA "P","R","S"
550 DATA "T","U","V"
560 DATA "W","X","Y"

