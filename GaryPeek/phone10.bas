100 REM MAKE WORDS FROM PHONE NUMBERS
110 GOSUB 530
120 DIM PHONE(20)
130 CLS:PRINT "ENTER 10 DIGIT PHONE NUMBER  ";
140 INPUT PHONE$
150 FOR DIGIT=1 TO LEN(PHONE$)
160   PHONE(DIGIT)=VAL(MID$(PHONE$,DIGIT,1))
170 NEXT DIGIT
180 FOR A=1 TO 3
190 A$=L$(PHONE(1),A)
200 FOR B=1 TO 3
210 B$=L$(PHONE(2),B)
220 FOR C=1 TO 3
230 C$=L$(PHONE(3),C)
240 FOR D=1 TO 3
250 D$=L$(PHONE(4),D)
260 FOR E=1 TO 3
270 E$=L$(PHONE(5),E)
280 FOR F=1 TO 3
290 F$=L$(PHONE(6),F)
300 FOR G=1 TO 3
310 G$=L$(PHONE(7),G)
320 FOR H=1 TO 3
330 H$=L$(PHONE(8),H)
340 FOR I=1 TO 3
350 I$=L$(PHONE(9),I)
360 FOR J=1 TO 3
370 J$=L$(PHONE(10),J)
380 PRINT A$+B$+C$+D$+E$+F$+G$+H$+I$+J$;"   ";
390 NEXT J
400 PRINT
410 NEXT I
420 NEXT H
430 NEXT G
440 NEXT F
450 NEXT E
460 NEXT D
470 NEXT C
480 NEXT B
490 NEXT A
500 IN$=INKEY$:IF IN$="" THEN 500
510 IF IN$=CHR$(27) THEN END
520 GOTO 130
530 DIM L$(9,4)
540 FOR A=0 TO 9
550 FOR B=1 TO 3
560 READ L$(A,B)
570 NEXT B
580 NEXT A
590 RETURN
600 DATA "?","?","?"
610 DATA "?","?","?"
620 DATA "A","B","C"
630 DATA "D","E","F"
640 DATA "G","H","I"
650 DATA "J","K","L"
660 DATA "M","N","O"
670 DATA "P","R","S"
680 DATA "T","U","V"
690 DATA "W","X","Y"

