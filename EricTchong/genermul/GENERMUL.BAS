10 ' genermul.bas - Freeware April 23, 2004
20 GOTO 90 ' begin
30 GOTO 560 ' get all results
40 GOTO 770 ' count length of the number
50 GOTO 800 ' wait for key
60 GOTO 840 ' send results to screen or diskfile
70 GOTO 880 ' empty strings
80 GOTO 900 ' clear line
90 CLS:KEY OFF:B=23
100 DEFDBL D,Z,E,V,M:RANDOMIZE TIMER
110 DIM A(B),VTP(B),L$(B),N$(B):M1=9999:M2=9999
120 SP$=STRING$(5,32):W$=MKI$(0):GOSUB 70
130 PRINT "ษอออออออออออออออออออออออออออออออป"
140 PRINT "บ MULTIPLICATION SUMS GENERATOR บ"
150 PRINT "บ Eric F. Tchong -------- Aruba บ"
160 PRINT "ศอออออออออออออออออออออออออออออออผ"
170 PRINT:PRINT "Do you need a diskfile: <y/n> ?":GOSUB 50
180 IF P=89 OR P=121 THEN COPY=1 ELSE COPY=0
190 IF COPY THEN CLS:GOTO 200 ELSE 240
200 LINE INPUT "Enter a DOS filename ? ";Z$:CLS
210 IF LEFT$(Z$,1)=" " THEN Z$=MID$(Z$,2):GOTO 210
220 OPEN "O",#1,Z$
230 ' Announce us
240 CLS:PRINT "MULTIPLICATION SUMS GENERATOR":PRINT
250 PRINT "1) Manual operation"
260 PRINT "2) Auto   operation"
270 PRINT "3) Quit program...."
280 PRINT:PRINT "Choose <1-3>"
290 GOSUB 50
300 IF P=49 THEN O=1 ELSE IF P=50 THEN O=2 ELSE IF P=51 THEN 920
310 IF O<1 OR O>2 THEN GOSUB 80:GOTO 240 ELSE GOSUB 80
320 CLS:ON O GOTO 340,480
330 ' Manual input
340 PRINT "Numbers < 9 999 999"
350 FOR SUM=1 TO 5:PRINT
360  PRINT "Sum ";SUM;
370  INPUT "Multiplicand <-1 Menu> ";VT
380  IF VT>9999999# OR VT<>INT(VT) THEN BEEP:GOSUB 80:GOTO 360
390  IF VT=-1 THEN 240 ELSE IF VT=0 THEN VT=M1
400  PRINT "Sum ";SUM;:INPUT "Multiplier   <-1 Menu> ";VV
410  IF VV>9999999# OR VV<>INT(VV) THEN BEEP:GOSUB 80:GOTO 400
420  IF INT(VT*VV)>99980001# THEN BEEP:GOSUB 80:GOTO 400
430  IF VV=-1 THEN 240 ELSE IF VV=0 THEN VV=M2
440  M1=VT:M2=VV:GOSUB 30
450 NEXT
460 CLS:GOSUB 60:GOSUB 70:TL=0:GOTO 340
470 ' Automatic multiplications generator
480 CLS:FOR SUM=1 TO 5:VT=INT(RND*998)+1:VV=INT(RND*998)+1:GOSUB 30:NEXT
490 GOSUB 60:GOSUB 70:TL=0
500 FOR SUM=1 TO 5:VT=INT(RND*9998)+1:VV=INT(RND*9998)+1:GOSUB 30:NEXT
510 GOSUB 60:GOSUB 70:TL=0
520 PRINT "Press <any key> or M = Menu";
530 GOSUB 50
540 IF P=77 OR P=109 THEN 240 ELSE 480
550 ' Get all results
560 VP=INT(VT*VV):IF VT<VV THEN SWAP VT,VV
570 Z=VP:GOSUB 40:LVP=L:Z=VV:GOSUB 40:LVV=L:D=0
580 FOR X=1 TO L-1:D=D+A(X):NEXT:IF D=0 THEN SW=1 ELSE SW=0 ' 10 200 3000
590 FOR X=1 TO L:A(X)=A(X)*(10^(X-1)):NEXT X
600 FOR X=L TO 1 STEP -1:VTP(X)=A(X)*VT:NEXT
610 FOR X=1 TO L:Z=VTP(X):GOSUB 40:LVTP(X)=L:NEXT
620 Z=VT:GOSUB 40:LVT=L:Z=VV:GOSUB 40:LVV=L
630 ' Save each sum in N$(T)
640 L=LVP-LVT:N$(1)=STRING$(L,32)+STR$(VT)+SP$
650 L=LVP-LVV:N$(2)=STRING$(L,32)+STR$(VV)+" x   ":T=3
660 IF LVV=1 OR SW=1 THEN 720
670 N$(T)=STRING$(LVP-LVT+1,32)+STRING$(LVT,"ฤ")+SP$
680 FOR X=1 TO LVV
690  IF VTP(X)=0 THEN 710 ELSE T=T+1
700  N$(T)=STRING$(LVP-LVTP(X),32)+STR$(VTP(X))+SP$
710 NEXT :T=T+1
720 N$(T)=STRING$(1,32)+STRING$(LVP,"ฤ")+SP$
730 T=T+1:N$(T)=STR$(VP)+SP$:L=LEN(N$(T)):IF T>TL THEN TL=T
740 FOR X=T+1 TO B:N$(X)=STRING$(L,32):NEXT
750 FOR X=1 TO B:L$(X)=L$(X)+N$(X):NEXT:RETURN
760 ' Isolate digits or count length of digits
770 L=1
780 E=INT(Z/10):A(L)=Z-10*E:IF E<>0 THEN L=L+1:Z=E:GOTO 780 ELSE RETURN
790 ' Wait for a key input
800 LSET W$=MKI$(0)
810 WHILE CVI(W$)=0:MID$(W$,1)=INKEY$:WEND
820 P=ASC(W$):RETURN
830 ' Send results to screen or diskfile
840 FOR X=1 TO TL+1:PRINT L$(X):NEXT
850 IF copy THEN 860 ELSE RETURN
860 FOR X=1 TO TL+1:PRINT #1, L$(X):NEXT:RETURN
870 ' Empty strings
880 FOR X=1 TO B:L$(X)="":N$(X)="":NEXT:RETURN
890 ' Clear line
900 PRINT CHR$(30);:PRINT STRING$(75,32):PRINT CHR$(30);:RETURN
910 ' Exit
920 KEY 5,"genermul.bas":KEY 6,CHR$(34)+",a":KEY ON:CLS:END
930 ' Assemble each sum in N$(T) and save it in L$(X)
940 ' N$(1)       8759   VT
950 ' N$(2)       1024 x VV
960 ' N$(3)       ฤฤฤฤ
970 ' N$(4)      35036   VTP(1)
980 ' N$(5)     175180   VTP(2)
990 ' N$(6)    8759000   VTP(4)
1000 ' N$(7)    ฤฤฤฤฤฤฤ 
1010 ' N$(8)    8969216   VP 
