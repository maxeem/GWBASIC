10 REM PRINT DETAIL BOWLER RECORDS       (DETAIL)
12 REM Developed by William W. Klaproth,  Moraga Ca.  94556    September 1982
20 REM MASTER FILE DESCRIPTIONS FOR ALL BOWLING PROGRAMS
30 OPEN "B:TEAMS.HDR" AS #1 LEN = 64
40 FIELD #1, 2 AS TM$, 30 AS NAM$, 2 AS RCNT$, 2 AS GAM$, 4 AS PINS$, 2 AS AVG$, 2 AS HIG$, 2 AS HIS$, 4 AS PTSW$, 4 AS PTSL$, 2 AS B1ID$, 2 AS B2ID$, 2 AS B3ID$, 2 AS B4ID$, 2 AS B5ID$
50 FIELD #1, 2 AS HDRID$, 30 AS LEAGUE$, 2 AS WEEKS$, 2 AS TEAMS$, 4 AS FILLER$, 2 AS BWLPERTM$, 2 AS NXTID$, 2 AS HCPBASE$, 4 AS HCPPCNT$
60 REM OPEN "B:BOWLERS.HDR" AS #2 LEN = 64
70 REM FIELD #2, 2 AS BBO$, 30 AS BNAM$, 2 AS BRCNT$, 2 AS BGAM$, 4 AS BPINS$, 2 AS BAVG$, 2 AS BHIG$, 2 AS BHIS$, 2 AS BHCP$, 2 AS BHIGH$, 2 AS BHISH$, 2 AS BTM$,2 AS GAM2$, 2 AS SER5$, 2 AS SER6$,2 AS CURSER$,2 AS AVGLSTYR$
80 OPEN "B:TEAM.DET" AS #3 LEN = 16
90 FIELD #3,2 AS DTM$, 2 AS TMO$, 2 AS TDAY$, 2 AS TG1$, 2 AS TG2$, 2 AS TG3$, 2 AS TSER$, 2 AS DPTSW$
100 OPEN "B:BOWLER.DET" AS #4 LEN = 16
110 FIELD #4,2 AS DBO$, 2 AS BMO$, 2 AS BDAY$, 2 AS BG1$, 2 AS BG2$, 2 AS BG3$, 2 AS BSER$
120 CLS
130 PRINT "DETAIL FILES PRINT PROGRAM"
140 GET #1,1
150 TEAMS% = CVI(TEAMS$)
160 BOWLERS% = CVI(NXTID$)
170 LPRINT " "
180 LPRINT "TEAM DETAIL RECORDS"
190 LPRINT "REC# TEAM   MO  DAY GAM1 GAM2 GAM3  SER  WON"
200 FOR T% = 1 TO (TEAMS% * 50 + 100)
210 GET #3,T%
220 DTM% = CVI(DTM$)
230 IF DTM% = 0 GOTO 320
240 TMO% = CVI(TMO$)
250 TDAY% = CVI(TDAY$)
260 TG1% = CVI(TG1$)
270 TG2% = CVI(TG2$)
280 TG3% = CVI(TG3$)
290 TSER% = CVI(TSER$)
300 DPTSW% = CVI(DPTSW$)
310 LPRINT USING "#### ";T%,DTM%,TMO%,TDAY%,TG1%,TG2%,TG3%,TSER%,DPTSW%
320 NEXT T%
330 LPRINT " "
340 LPRINT "BOWLER DETAIL RECORDS"
350 LPRINT "REC#  BO#   MO  DAY GAM1 GAM2 GAM3  SER"
360 FOR B% = 1 TO (BOWLERS% * 50 + 100)
370 GET #4,B%
380 DBO% = CVI(DBO$)
390 IF DBO% = 0 GOTO 470
400 BMO% = CVI(BMO$)
410 BDAY% = CVI(BDAY$)
420 BG1% = CVI(BG1$)
430 BG2% = CVI(BG2$)
440 BG3% = CVI(BG3$)
450 BSER% = CVI(BSER$)
460 LPRINT USING "#### ";B%,DBO%,BMO%,BDAY%,BG1%,BG2%,BG3%,BSER%
470 NEXT B%
480 PRINT "Detail Printing Completed - Returning to MENU"
490 CLOSE
500 RUN "A:MENU"
