1000 '"MONOSET" A Program to test mono screen attributes in Basic
1010 REV$="Rev: 830120"'By Robert Hamilton
1020 SCREEN 0,0,0:WIDTH 80:COLOR 2,0,0:KEY OFF:CLS:UP$=STRING$(1,30)
1030 LOCATE ,,1,12,13:DEF SEG=0:IF PEEK (&H410)= 109 THEN LOCATE ,,,6,7
1040 DEF SEG=64:POKE 23,32:DEF SEG:CL$=STRING$(79,0):Q$=CHR$(34)
1050 LOCATE 5,32:PRINT"M O N O S E T";CHR$(10);TAB(33);REV$:COLOR 6,0
1060 LOCATE 14,22:COLOR 6,0:PRINT "FOREGROUND COLORS:  0 to 15"
1070 LOCATE 16,22:PRINT "ADD 8 to Forground for ";:COLOR 10,0:PRINT"HIGH INTENSITY"
1080 LOCATE 18,22:COLOR 6,0:PRINT "ADD 16 to Forground for ";:COLOR 26,0:PRINT"BLINKING"
1090 LOCATE 20,22:COLOR 6,0:PRINT "BACKGROUND COLORS:  0 to 7 "
1100 LOCATE 22,1:COLOR 6,0:PRINT STRING$(79,0);:LOCATE 22,1
1110 LOCATE 23,1:PRINT STRING$(79,0);:GOSUB 1260:LOCATE 23,1:COLOR 10,0
1120 LINE INPUT;" COLOR: Forground,Bacground (Q=End) ===> ",F$
1130 IF INSTR(F$,"Q")<>0 OR INSTR(F$,"q")<>0 THEN 1250
1140 IF F$="" THEN BEEP:GOTO 1100
1150 SEP=INSTR(F$,","):SEP2=INSTR(SEP+1,F$,","):IF SEP=0 THEN F=VAL(F$):D=VAL(F2$):GOTO 1200
1160 F1$=LEFT$(F$,SEP-1):IF F1$<>"" THEN F=VAL(F1$)
1170 IF SEP2=0 THEN F2$=MID$((F$),SEP+1):B=VAL(F2$):GOTO 1200
1180 F2$=MID$((F$),SEP+1,SEP2-1):IF LEFT$(F2$,1)<>"" AND LEFT$(F2$,1)<>"," AND LEFT$(F2$,1)<>" " THEN B=VAL(F2$)
1190 F3$=MID$((F$),SEP2+1):IF LEFT$(F3$,1)<>"" AND LEFT$(F3$,1)<>"," AND LEFT$(F3$,1)<>" " THEN BOR=VAL(F3$)
1200 IF F<0 OR F>31 THEN BEEP:GOTO 1240
1210 IF B<0 OR B>7 THEN BEEP:GOTO 1240
1220 IF BOR<0 OR BOR>15 THEN BEEP:GOTO 1240
1230 PRINT TAB(55);:COLOR F,B:PRINT" THIS IS A TEST ":COLOR 10,0:PRINT CHR$(10)
1240 GOTO 1100
1250 DEF SEG=64:POKE 23,128:WIDTH 80:COLOR 6,0:CLS:LOCATE 21,1:PRINT "DONE":END
1260 LOCATE 24,1:PRINT SPC(1);STRING$(74,"=");
1270 LOCATE 25,1
1280 COLOR 7,0:PRINT" COLOR  7,0   ";
1290 COLOR 23,0:PRINT"23,0   ";
1300 COLOR 10,0:PRINT"10,0   ";
1310 COLOR 28,0:PRINT"28,0   ";
1320 COLOR 1,0:PRINT"1,0";:COLOR 7,0:PRINT SPC(3);
1330 COLOR 17,0:PRINT"17,0";:COLOR 7,0:PRINT SPC(3);
1340 COLOR 9,0:PRINT"9,0";:COLOR 7,0:PRINT SPC(3);
1350 COLOR 25,0:PRINT"25,0";:COLOR 7,0:PRINT SPC(3);
1360 COLOR 0,7:PRINT" 0,7 ";:COLOR 7,0:PRINT SPC(3);
1370 COLOR 16,7:PRINT" 16,7 ";:COLOR 7,0:PRINT SPC(3);
1380 RETURN
