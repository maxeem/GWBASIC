5 'SAVE "MENU"
10 '
12 ' ISDDRIVR - ISD DEMO DRIVER
18 ' AUTHOR - Hal W. Jennings
20 '
22 'DEMO DRIVER
24 '
35 GOSUB 2800  'INIT
40 GOSUB 1000  'DISPLAY NEXT SCREEN
45 IF INKEY$<>"" THEN 45  'CLEAR KEYBOARD BUFFER
50 IF DELAY THEN GOSUB 200 ELSE GOSUB 800  'DELAY/INPUT
55 IF TERM THEN CHAIN "MENU"
60 IF RESTART THEN NXT$=FIRSTPROG$: NMP=0: GOTO 330
65 GOTO 40
200 '
205 'DELAY LOGIC
210 '
215 T!=FNTM!(TIME$)+DSEC: IF T!>86400! THEN T!=DSEC
220 K$=INKEY$: IF K$="" AND T!>FNTM!(TIME$) THEN 220
225 IF K$=BS$ THEN 560    'F9 - BACKUP SCREEN
230 IF K$=BM$ THEN 510    'F10- BACKUP MENU
235 IF K$=TERM$ THEN  460 'CTRL END - TERMINATE
240 IF K$=ID$ THEN GOSUB 940: GOTO 220    'F8 - DISPLAY ID
245 IF K$=HELP$ THEN  GOSUB 970: GOTO 215 'F1 - HELP
250 IF K$=" " THEN 410    'SPACE BAR - PAUSE
255 IF K$<>"" THEN 220    'INVALID KEY - TIME NOT EXPIRED
300 NMP=NMP+1: NM$(NMP)=CSCR$
305 IF NMP>148 THEN I=1:FOR J=30 TO NMP:NM$(I)=NM$(J):I=I+1:NEXT J:NMP=I-1
310 IF LEFT$(NEXTSCR$,1)<>"&" THEN RETURN
315 NXT$=MID$(NEXTSCR$,2): IF NXT$="END" THEN TERM=TRUE: RETURN
320 IF NXT$="RETURN" THEN NEXTSCR$=NM$(NMP-1): NMP=NMP-2: IF LEFT$(NEXTSCR$,1)="," THEN NEXTSCR$=MID$(NEXTSCR$,2): RETURN ELSE RETURN
325 NMP=NMP+1: NM$(NMP)="&"+THISPROG$
330 LOCATE 25,1: COLOR 15,0: PRINT SPC(38);: LOCATE 25,6: PRINT "** PROGRAM ";NXT$;" LOADING **";
335 ON ERROR GOTO 340: CHAIN "A:"+NXT$
340 RESUME 345
345 ON ERROR GOTO 350: CHAIN "B:"+NXT$
350 CLS: LOCATE 10,5: PRINT "PROGRAM ";NXT$;" NOT FOUND": LOCATE 23,5: PRINT "Press any key to end demo"
355 IF INKEY$="" THEN 355 ELSE TERM=-1: GOTO 55
400 '
405 'SPACE BAR (PAUSE)
410 IF INKEY$="" THEN 410 ELSE 300
450 '
455 'CTRL END KEY (EXIT)
460 TERM=TRUE: RETURN
500 '
505 'BACKUP MENU
510 BKUPM=TRUE
515 IF INSTR(",&",LEFT$(NM$(NMP),1))=0 AND NMP>1 THEN NMP=NMP-1: GOTO 515
550 '
555 'BACKUP SCREEN
560 IF NMP<=0 THEN NEXTSCR$=CSCR$ ELSE NEXTSCR$=NM$(NMP):NMP=NMP-1: BKUP=TRUE
565 IF LEFT$(NEXTSCR$,1)="," THEN NEXTSCR$=MID$(NEXTSCR$,2): RETURN
570 IF LEFT$(NEXTSCR$,1)="&" THEN NXT$=MID$(NEXTSCR$,2): GOTO 330
575 RETURN
800 '
805 'INPUT LOGIC
810 '
815 T!=FNTM!(TIME$)+120: IF T!>86499! THEN T!=120
820 K$=INKEY$: IF K$="" AND T!>FNTM!(TIME$) THEN 820
825 IF K$=BS$ THEN 560    'F9 - BACKUP SCREEN
830 IF K$=BM$ THEN 510    'F10- BACKUP MENU
835 IF K$=TERM$ THEN  460 'CTRL END - TERMINATE
840 IF K$=ID$ THEN GOSUB 940: GOTO 820    'F8 - DISPLAY ID
845 IF K$=HELP$ THEN  GOSUB 970: GOTO 800 'F1 - HELP
850 IF (K$=ESC$) AND TIM THEN RESTART=TRUE: RETURN  'ESC KEY WITH TIME LIMIT
855 IF K$="" THEN GOSUB 910: GOTO 820  'TIME EXPIRED
860 S=ASC(K$): IF S>96 AND S<123 THEN K$=CHR$(S-32)
865 ANS=INSTR(BRANCH$,B$+K$): IF ANS=0 THEN ANS=INSTR(BRANCH$,BA$)
870 IF ANS=0 THEN BEEP: GOTO 820
875 I=INSTR(ANS,BRANCH$,D$): J=INSTR(I,BRANCH$,B$): NEXTSCR$=MID$(BRANCH$,I+1,J-I-1): IF INSTR(2,BRANCH$,B$)<LEN(BRANCH$) THEN CSCR$=","+CSCR$
880 GOTO 300
900 '
905 'TIME EXPIRED ON INPUT SCREEN
910 LOCATE 25,1: COLOR 15,0: PRINT" OR PRESS THE 'Esc' KEY TO START OVER ";:T!=86500!: TIM=TRUE: RETURN
930 '
935 'DISPLAY PROGRAM AND SCREEN NAME
940 LOCATE 25,1: COLOR 15,0: PRINT"  PROG=";THISPROG$;"   SCREEN=";CSCR$;"  ";: RETURN
960 '
965 'HELP
970 LOCATE 25,1: COLOR 15,0: PRINT" F9=BACKUP  F10=BACKUP TO PREV. MENU ";: RETURN
1000 '
1002 'PLACE TESTS FOR SCREEN NAMES HERE
1004 '
1006 CSCR$=NEXTSCR$: BKUP=FALSE: BKUPM=FALSE: TIM=FALSE
1010 IF CSCR$="MENU1" THEN 3000
1020 IF CSCR$="MENU2" THEN 3027
2700 '
2705 'SCREEN NOT FOUND
2710 '
2715 CLS: LOCATE 10,5: PRINT "SCREEN ";NEXTSCR$;" NOT FOUND": LOCATE 23,5: PRINT "Press any key to end demo"
2720 DELAY=FALSE: BRANCH$="þ*$&ENDþ": RETURN
2800 '
2805 'INIT (INITIALIZATION SUBROUTINE)
2810 '
2815 DEFINT A-Z: KEY OFF: SCREEN 0,1,0,0
2820 K$="": B$="þ": FALSE=0: TRUE=NOT FALSE: Q$=CHR$(34): ESC$=CHR$(27): TERM=FALSE: RESTART=FALSE: BA$="þ*": D$="$"
2825 BS$=CHR$(0)+CHR$(67): BM$=CHR$(0)+CHR$(68): HELP$=CHR$(0)+CHR$(59): TERM$=CHR$(0)+CHR$(117): ID$=CHR$(0)+CHR$(66)
2830 DEF FNTM!(T$)=VAL(LEFT$(T$,2))*3600!+VAL(MID$(T$,4,2))*60!+VAL(RIGHT$(T$,2))
2835 'DISABLE FUNCTION KEYS
2840 FOR I=1 TO 10: KEY I,N$: NEXT
2845 READ FIRSTPROG$,THISPROG$
2850 'SCREEN STACK
2855 COMMON NM$(),NMP,BKUP,BKUPM,IBMBW,NEXTSCR$,NXT$
2860 IF NEXTSCR$="" THEN DIM NM$(150)
2865 IF BKUPM THEN 510 ELSE IF BKUP THEN 560
2870 READ NEXTSCR$  'NAME OF FIRST SCREEN
2875 'TEST FOR IBM B&W DISPLAY
2880 DEF SEG=0:IF (PEEK(&H410) AND &H30)=&H30 THEN IBMBW=TRUE:LOCATE ,,,12,13 ELSE IBMBW=FALSE:LOCATE ,,,6,7
2885 RETURN
2890 'FIRSTPROG$,THISPROG$,NEXTSCR$
2999 DATA MENU,MENU,MENU1
3000 'SAVE "B:DYINT000.ISD",A 'TITLE=IBM logo in blocks
3003 WIDTH 80:COLOR 7,0,0:CLS
3006 LOCATE 2,34:PRINT USING "&";"INTRODUCING...";:LOCATE 5,39:PRINT USING "&";"The";:LOCATE 8,17:PRINT USING "&";"ßßßßßßßß   ßßßßßßßßßßßß     ßßßßßßß       ßßßßßßß";:LOCATE 9,17:PRINT USING "&";"ß";
3009 PRINT USING "&";"ßßßßßßß   ßßßßßßßßßßßßßß   ßßßßßßßß     ßßßßßßßß";:LOCATE 10,19:PRINT USING "&";"ßßßß       ßßßß    ßßßß     ßßßßßßß   ßßßßßßß";:LOCATE 11,19:PRINT USING "&";"ß";
3012 PRINT USING "&";"ßßß       ßßßßßßßßßß       ßßßßßßßß ßßßßßßßß";:LOCATE 12,19:PRINT USING "&";"ßßßß       ßßßßßßßßßß       ßßßß ßßßßßßß ßßßß";:LOCATE 13,19:PRINT USING "&";"ßßßß       ß";
3015 PRINT USING "&";"ßßß    ßßßß     ßßßß  ßßßßß  ßßßß";:LOCATE 14,17:PRINT USING "&";"ßßßßßßßß   ßßßßßßßßßßßßßß   ßßßßßß   ßßß   ßßßßßß";:LOCATE 15,17:PRINT USING "&";"ßßßßßßßß   ß";
3018 PRINT USING "&";"ßßßßßßßßßßß     ßßßßßß    ß    ßßßßßß";:LOCATE 18,32:PRINT USING "&";"Personal  Computer";
3021 COLOR 15,0:LOCATE 24,1:PRINT USING "&";"Press the Space bar";
3024 LOCATE 24,19,0:DELAY=0:BRANCH$="þ*$MENU2þ":RETURN
3027 'SAVE "B:MENU2.ISD",A 'TITLE=Dealer Demo Menu
3030 WIDTH 40:COLOR 7,0,0:CLS
3033 IF IBMBW THEN OUT 948,2:OUT 949,62
3036 COLOR 0,7:LOCATE 1,7:PRINT USING "&";" ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿ ";
3039 LOCATE 2,7:PRINT USING "&";" ³ IBM Personal Computer ³ ";
3042 LOCATE 3,7:PRINT USING "&";" ³ Demonstration Series  ³ ";
3045 LOCATE 4,7:PRINT USING "&";" ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ ";
3048 COLOR 7,0:LOCATE 6,7:PRINT USING "&";"Select one of the following:";:LOCATE 9,10:PRINT USING "&";"1. Unattended Demo";:LOCATE 11,10:PRINT USING "&";"2. Do-It-Yourself Demo";:LOCATE 13,10:PRINT USING "&";"3";
3051 PRINT USING "&";". Samples";:LOCATE 15,10:PRINT USING "&";"4. Communications";:LOCATE 17,10:PRINT USING "&";"5. Functional Demo";:LOCATE 19,10:PRINT USING "&";"6. Switch to colo";
3054 PRINT USING "&";"r monitor";:LOCATE 21,10:PRINT USING "&";"7. Switch to IBM Monochrome";
3057 COLOR 0,7:LOCATE 23,10:PRINT USING "&";" ";
3060 COLOR 7,0:PRINT USING "&";"  Option";
3063 LOCATE 23,10,1:DELAY=0:BRANCH$="þ1$&TEXTþ2$&IBMDIYþ3$&SAMPLESþ4$&COMMþ5$&DEMO3þ6$&Cþ7$&Bþ":RETURN
