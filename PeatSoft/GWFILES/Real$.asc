1000  PROG1$="     REAL$     " 'PRODUCES A REAL ESTATE APPRECIATION TABLE
1010  REV$ = "  Rev: 830120  "' By Robert Hamilton
1020  DEFDBL A,P : DEFINT I,S,Y
1030  SCREEN 0,0,0:WIDTH 80:COLOR 2,0,0:KEY OFF:CLS:UP$=STRING$(1,30)
1040  LOCATE ,,1,12,13:DEF SEG=0:IF PEEK (&H410)= 109 THEN LOCATE ,,,6,7
1050  DEF SEG=64:POKE 23,128:DEF SEG:CL$=STRING$(79,0):Q$=CHR$(34)
1060  CLS:LOCATE 6,30:PRINT PROG1$
1070  PRINT CHR$(10);TAB(30);REV$:COLOR 6,0
1080  LOCATE 25,1:PRINT CL$;:LOCATE 25,1
1090  PRINT"ENTER  Market Value in 1980 Dollars  (Q = Quit)==> ";:INPUT"",AMOUNT$
1100  IF AMOUNT$="q" OR AMOUNT$="Q" THEN END
1110  IF AMOUNT$="" THEN BEEP:GOTO 1080
1120  AMOUNT=VAL(AMOUNT$)
1130  IF AMOUNT <1 OR AMOUNT >9999999! THEN BEEP:GOTO 1080
1140  P1=AMOUNT : P2=AMOUNT : P3=AMOUNT : P4=AMOUNT : P5=AMOUNT
1150  P10=0 : P20=0 : P30=0 : P40=0 : P50=0 : SKIP=0 : YEAR=1980
1160  CLS:LOCATE 25,1,1:COLOR 0,7:PRINT" ENTER ";:COLOR 5,0:PRINT" For CRT    ";
1170  COLOR 0,7:PRINT" P ";:COLOR 5,0:PRINT" For Printer Copy ";:P$="SCRN:":YR=15:PRT=0:COLOR 6,0
1180  K$=INKEY$:IF MID$(K$,1,1)="P" OR MID$(K$,1,1)="p" THEN P$="LPT1:":PRT=1:YR=45
1190 IF K$="" THEN 1180
1200  OPEN P$ FOR OUTPUT AS #1 :CLS:IF PRT=1 THEN PRINT #1,CHR$(10);CHR$(10)
1210  LOCATE 25,1:COLOR 6,0:PRINT "WAIT - Printing":IF PRT=0 THEN LOCATE 1,1
1220  PRINT #1,TAB(28);"REAL  ESTATE  APPRECIATION" : PRINT #1,""
1230  PRINT #1," YEAR             8%           10%           12%           14%           16%" : PRINT #1,""
1240    FOR I=1 TO YR
1250    P1=P1+P10 : P2=P2+P20 : P3=P3+P30 : P4=P4+P40 : P5=P5+P50
1260    PRINT #1,YEAR; : PRINT #1,USING " ############,.";P1,P2,P3,P4,P5
1270    SKIP=SKIP+1 :IF SKIP=5 THEN SKIP=0 :IF SKIP=0 THEN PRINT #1,""
1280    P10=P1*.08 :P20=P2*.1 :P30=P3*.12 :P40=P4*.14 :P50=P5*.16 :YEAR=YEAR+1
1290    NEXT
1300  IF PRT=1 THEN PRINT #1,STRING$(4,10)
1310  CLOSE #1 : KEY ON : BEEP : END
