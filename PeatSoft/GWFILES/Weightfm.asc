5 CLS:KEY OFF:SCREEN 0,0,0:WIDTH 80:CLEAR:FOR I=1 TO 10:KEY I,"":NEXT:KEY 2,"GOTO 60"+CHR$(13):KEY 10,"RUN"+CHR$(34)+"BLUEMENU"+CHR$(34)+CHR$(13):LIST 10-57
10 '***************************************************************************
16 'ษอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออป
18 'บ                  DAILY WEIGHT RECORD "MOTIVATION" FORM                  บ
20 'บ                                                                         บ
22 'บ    The form generated by this program can be used to maintain daily or  บ
24 'บ other periodic health records.  The form is designed primarily for a    บ
26 'บ weight record; however, the word "WEIGHT" can be replaced easily by any บ
28 'บ other word or dimension for which a daily or periodic record might be   บ
30 'บ appropriate.  The form can be modified easily to increase or decrease   บ
31 'บ the number of lines by making an appropriate change in the number of    บ
32 'บ loops printed.  The loop size entry 27 and is currently set at 24 to    บ
33 'บ make the form the right length with its present format and content.     บ
34 'บ To run the form as designed, simply set print head at the first line    บ
35 'บ below the perforation and ascertain the printer is ON LINE. Press <F2>. บ
36 'บ                   (c) G I N A C O    1983              Ver 5.1/160/320  บ
38 'ศอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผ
55 '        *****  < F2 > to RUN  *******  < F10 > For BLUEMENU  *****
57 ' ====== To avoid DOCUMENTATION BOX each time, REM or DELETE  line 5 ======
60 CLS:KEY OFF:SCREEN 0,0,0:WIDTH 80:CLEAR:FOR I=1 TO 10:KEY I,"":NEXT
80 REM ===== INITIALIZE PRINTER CODES =====
90 GOSUB 5000
100 ON ERROR GOTO 6000
110 REM ===== PUT IN FORM HEADING =====
120 LPRINT CLEANUP$;
130 WIDTH "LPT1:",96
140 LPRINT DARK$;
150 LPRINT STRING$(4,10);
160 LPRINT EXPAND$;STRING$(39,42)
170 LPRINT
180 LPRINT EXPAND$; TAB( 2);"D A I L Y    W E I G H T    C H A R T"
190 LPRINT
200 LPRINT EXPAND$;STRING$(39,42)
210 LPRINT
220 LPRINT TAB( 3);EXPAND.SMALL$"START WEIGHT:"START.ULINE$"_         _";END.ULINE$"_           _";"DESIRED WEIGHT:"START.ULINE$"_         _";END.ULINE$
230 LPRINT CLEANUP$;
240 LPRINT DARK$
250 LPRINT EXPAND$;STRING$(39,"=")
260 LPRINT ONEWAY$;
270 FOR I = 1 TO 22
280 LPRINT EXPAND.SMALL$;"DATE: "START.ULINE$"_      _"END.ULINE$;"   ";"WEIGHT: "START.ULINE$"_     _"END.ULINE$;" || ";"DATE: "START.ULINE$"_     _"END.ULINE$;"   ";"WEIGHT: "START.ULINE$"_     _"END.ULINE$
290 LPRINT EXPAND$;TAB(34);"||"
300 NEXT
310 LPRINT
320 LPRINT EXPAND$;TAB(15)"MONTH: "START.ULINE$"_        _"END.ULINE$;"        ";"YEAR: "START.ULINE$"_        _"END.ULINE$
330 LOCATE 10,30:PRINT"THE FORM IS PRINTING"
340 LPRINT CHR$(12):END
5000 '========== PRINTER CONTROL SUBROUTINE ==========
5030 SMALL$ = CHR$(15)
5060  UNSMALL$ = CHR$(18)
5090   EXPAND.SMALL$ = CHR$(14) + CHR$(15)
5120    CANX.EXP.COMP$ = CHR$(18) + CHR$(20)
5150     EXPAND$ = CHR$(14)
5180      EXPAND.END$ = CHR$(20)
5210       DARK$ = CHR$(27) + CHR$(69)
5240        ITALIC$ = CHR$(27) + CHR$(52)
5270         ITALIC.EXP.CON$ = CHR$(14) + CHR$(15) + CHR$(27) + CHR$(52)
5300 ITALIC.EXP$ = CHR$(27) + CHR$(52) + CHR$(14)
5330  END.ITALIC$ = CHR$(27) + CHR$(53)
5360   START.ULINE$ = CHR$(27) + CHR$(45) + CHR$(1)
5390    END.ULINE$ = CHR$(27) + CHR$(45) + CHR$(0)
5420     PERM.EXPAN$ = CHR$(27) + CHR$(87) + CHR$(1)
5450      END.PERMEXP$ = CHR$(27) + CHR$(87) + CHR$(0)
5480 START.DSTRIKE$ = CHR$(27) + CHR$(71)
5510  END.DSTRIKE$ = CHR$(27) + CHR$(72)
5540   TINYLINE$ = CHR$(27) + CHR$(83) + CHR$(0)
5570    END.TINY$ = CHR$(27) + CHR$(84)
5600     ESC$ = CHR$(27)
5630      ONEWAY$ = CHR$(27) + CHR$(85) + CHR$(1)
5660       END.ONEWAY$ = CHR$(27) +CHR$(85) + CHR$(0)
5680        CLEANUP$ = CHR$(27) + CHR$(64)
5690         RETURN
6000 REM ********* PRINTER ERROR ROUTINE ***************
6030 IF ERR = 24 THEN 6120 ELSE 6060
6060 IF ERR = 25 THEN 6120 ELSE 6090
6090 ON ERROR GOTO 0
6120 KEY OFF
6150  BEEP
6180   BEEP
6210    LOCATE 25,1
6240     PRINT SPC( 79)
6270      LOCATE 25,20
6300       PRINT "CHECK PRINTER - PRESS ANY KEY TO CONTINUE"
6330 IF INKEY$ = "" THEN 6330
6360 KEY OFF
6390  RESUME
6420   END
65000 REM ==========  SAVE ROUTINE  ==========
65100 SAVE "B:WEIGHTFM.BAS"
