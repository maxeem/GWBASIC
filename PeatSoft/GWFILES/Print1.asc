10 REM PRINT DISK FILE HEADERS PROGRAM        (PRINT)
12 REM Developed by William W. Klaproth,  Moraga Ca.  94556    September 1982
20 REM MASTER FILE DESCRIPTIONS FOR ALL BOWLING PROGRAMS
30 OPEN "B:TEAMS.HDR" AS #1 LEN = 64
40 FIELD #1, 2 AS TM$, 30 AS NAM$, 2 AS RCNT$, 2 AS GAM$, 4 AS PINS$, 2 AS AVG$, 2 AS HIG$, 2 AS HIS$, 4 AS PTSW$, 4 AS PTSL$, 2 AS B1ID$, 2 AS B2ID$, 2 AS B3ID$, 2 AS B4ID$, 2 AS B5ID$
50 FIELD #1, 2 AS HDRID$, 30 AS LEAGUE$, 2 AS WEEKS$, 2 AS TEAMS$, 4 AS FILLER$, 2 AS BWLPERTM$, 2 AS NXTID$, 2 AS HCPBASE$, 4 AS HCPPCNT$
60 OPEN "B:BOWLERS.HDR" AS #2 LEN = 64
70 FIELD #2, 2 AS BBO$, 30 AS BNAM$, 2 AS BRCNT$, 2 AS BGAM$, 4 AS BPINS$, 2 AS BAVG$, 2 AS BHIG$, 2 AS BHIS$, 2 AS BHCP$, 2 AS BHIGH$, 2 AS BHISH$, 2 AS BTM$,2 AS GAM2$, 2 AS SER5$, 2 AS SER6$,2 AS CURSER$,2 AS AVGLSTYR$
80 REM OPEN "B:TEAM.DET" AS #3 LEN = 16
90 REM FIELD #3,2 AS DTM$, 2 AS TMO$, 2 AS TDAY$, 2 AS TG1$, 2 AS TG2$, 2 AS TG3$, 2 AS TSER$, 2 AS DPTSW$
100 REM OPEN "B:BOWLER.DET" AS #4 LEN = 16
110 REM FIELD #4,2 AS DBO$, 2 AS BMO$, 2 AS BDAY$, 2 AS BG1$, 2 AS BG2$, 2 AS BG3$, 2 AS BSER$
120 CLS
130 PRINT "PRINT MASTER FILE HEADERS"
140 PRINT " "
150 DIM BNUM%(5)
160 GET #1,1
170 TM% = CVI(TEAMS$)
180 BO% = CVI(BWLPERTM$)
190 TEAM% = CVI(HDRID$)
200 WEEKS% = CVI(WEEKS$)
210 FILL = CVS(FILLER$)
220 NXT% = CVI(NXTID$)
230 BASE% = CVI(HCPBASE$)
240 PCNT = CVS(HCPPCNT$)
250 LPRINT "LEAGUE HEADER: ";NAM$
260 LPRINT "REC#  WKS  TMS       B/T  NXT BASE   %%"
270 LPRINT USING "#### ";TEAM%,WEEKS%,TM%,FILL,BO%,NXT%,BASE%,PCNT
280 FOR T% = 1 TO TM%
290 GET #1, (T% + 1)
300 TEAM% = CVI(TM$)
310 RCNT% = CVI(RCNT$)
320 GAM% = CVI(GAM$)
330 PINS = CVS(PINS$)
340 AVG% = CVI(AVG$)
350 HIG% = CVI(HIG$)
360 HIS% = CVI(HIS$)
370 PTSW = CVS(PTSW$)
380 PTSL = CVS(PTSL$)
390 BNUM%(1) = CVI(B1ID$)
400 BNUM%(2) = CVI(B2ID$)
410 BNUM%(3) = CVI(B3ID$)
420 BNUM%(4) = CVI(B4ID$)
430 BNUM%(5) = CVI(B5ID$)
440 LPRINT "***************************************************************************"
450 LPRINT "TEAM HEADER: ";NAM$
460 LPRINT " TM#  CNT  GAM PINS  AVG  HIG  HIS PTSW PTSL  B#1  B#2  B#3  B#4  B#5"
470 LPRINT USING "#### ";TEAM%,RCNT%,GAM%,PINS,AVG%,HIG%,HIS%,PTSW,PTSL,BNUM%(1),BNUM%(2),BNUM%(3),BNUM%(4),BNUM%(5)
480 LPRINT " "
490 LPRINT "BOWLER HEADERS"
500 LPRINT "  NO  CNT  GAM  PIN  AVG  HIG  HIS  HCP  HHG  HHS  TM#  200  500  600  SER"
510 FOR B% = 1 TO BO%
520 BREC% = BNUM%(B%)
530 GET #2,BREC%
540 BBO% = CVI(BBO$)
550 BRCNT% = CVI(BRCNT$)
560 BGAM% = CVI(BGAM$)
570 BPINS = CVS(BPINS$)
580 BAVG% = CVI(BAVG$)
590 BHIG% = CVI(BHIG$)
600 BHIS% = CVI(BHIS$)
610 BHCP% = CVI(BHCP$)
620 BHIGH% = CVI(BHIGH$)
630 BHISH% = CVI(BHISH$)
640 BTM% = CVI(BTM$)
650 LPRINT "BOWLER HEADER: ";BNAM$
660 LPRINT USING "#### ";BBO%,BRCNT%,BGAM%,BPINS,BAVG%,BHIG%,BHIS%,BHCP%,BHIGH%,BHISH%,BTM%,CVI(GAM2$),CVI(SER5$),CVI(SER6$),CVI(CURSER$)
670 NEXT B%
680 NEXT T%
690 LPRINT "***************************************************************************"
700 LPRINT "SUBSTITUTE RECORDS"
710 LPRINT "  NO  CNT  GAM  PIN  AVG  HIG  HIS  HCP  HHG  HHS  TM#  200  500  600  SER"
720 FOR SREC% = (BREC%+1) TO (NXT%-1)
730 GET #2,SREC%
740 BBO% = CVI(BBO$)
750 BRCNT% = CVI(BRCNT$)
760 BGAM% = CVI(BGAM$)
770 BPINS = CVS(BPINS$)
780 BAVG% = CVI(BAVG$)
790 BHIG% = CVI(BHIG$)
800 BHIS% = CVI(BHIS$)
810 BHCP% = CVI(BHCP$)
820 BHIGH% = CVI(BHIGH$)
830 BHISH% = CVI(BHISH$)
840 BTM% = CVI(BTM$)
850 LPRINT "NAME: ";BNAM$
860 LPRINT USING "#### ";BBO%,BRCNT%,BGAM%,BPINS,BAVG%,BHIG%,BHIS%,BHCP%,BHIGH%,BHISH%,BTM%,CVI(GAM2$),CVI(SER5$),CVI(SER6$),CVI(CURSER$)
870 NEXT SREC%
880 LPRINT "END OF FILE"
890 PRINT "Header Printing Completed - Returning to MENU"
900 CLOSE
910 RUN "A:MENU"
