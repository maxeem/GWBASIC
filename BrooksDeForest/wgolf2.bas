1 REM WORLD GOLF 2 - TANDY 1000 - JOYSTICK VERSION
2 REM COPYRIGHT 1990 BROOKS DEFOREST
4 RANDOMIZE TIMER
5 SCREEN 4:SCREEN 3:KEY OFF:VIEW PRINT 1 TO 25:FOR P=0 TO 15:PALETTE P,P:NEXT:COLOR 7,8:CLS
6 FOR P=0 TO 15:PALETTE P,0:NEXT:DIM B1(9),B2(9),B3(9),B4(9):CIRCLE(5,5),3,7:PAINT(5,5),7:GET(2,2)-(8,8),B1:CLS:CIRCLE(5,5),1,7:PAINT(5,5),7:GET(2,2)-(8,8),B2:CLS:CIRCLE(5,5),0,7:GET(2,2)-(8,8),B3:CLS
7 DIM A(20):LINE(0,0)-(8,10),4:LINE(8,0)-(0,10),4:PSET(4,5),0:GET(0,0)-(8,10),A
8 DIM B(800):DIM C(100)
9 SCREEN 2:SCREEN 3:COLOR 7,8
11 MD=1
12 PLAY "MB":SOUND ON
14 GOTO 40
15 LINE(0,10)-(159,80),9,BF:LINE(0,81)-(159,170),2,BF:B=4:FOR A=1 TO 8:LINE(0,77+B)-(159,77+B),10,,A*99:LINE(-20,77+B)-(169,77+B),10,,A*99:B=B+B/2:NEXT
16 LINE(0,81)-(159+RND*20,81),9,,RND*30000
24 FOR A=1 TO 2+RND*5
25 C1=-10+RND*169:C2=15+RND*50:LINE(C1,C2)-(C1+20,C2),11:LINE(C1-5,C2-1)-(C1+14,C2-1),11:LINE(C1-2,C2-2)-(C1+9,C2-2),3:LINE(C1+9,C2+1)-(C1+17,C2+1),3
26 NEXT
30 IF SHT=0 THEN LINE(50,170)-(50,160),4:CIRCLE(50,160),6,1:PAINT(50,160),1:CIRCLE(50,160),6,9:LINE(110,170)-(110,160),4:CIRCLE(110,160),6,1:PAINT(110,160),1:CIRCLE(110,160),6,9
39 RETURN
40 GOSUB 15
45 LINE(40,140)-(44,90),15,B:LINE(115,140)-(119,90),15,B:LINE(41,140)-(43,90),7,BF:LINE(116,140)-(118,90),7,BF:LINE(35,51)-(125,90),15,B:LINE(36,52)-(124,89),0,BF
50 COLOR 7,8:LOCATE 8,5:PRINT "WORLD GOLF":LOCATE 10,8:PRINT "C 1990"
51 M$="(C)1990 RUFUS PRO :: PROGRAM BY B.DEFOREST :: USE JOYSTICK FOR CONTROL                          "
52 M=0:MM=LEN(M$):Q=0:COLOR 8:PALETTE 8,0
53 M=M+1:IF M>MM THEN M=1
54 GET(9,0)-(152,7),B:PUT(8,0),B,PSET:Q=Q+1:IF Q=8 THEN Q=0:LOCATE 1,19:PRINT MID$(M$,M,1):GOTO 53
56 IF INKEY$=CHR$(27) THEN SCREEN 2:SCREEN 0:CHDIR "..":RUN "menu"
57 IF STRIG(1)<>-1 THEN 54
60 CLS
61 PLAY "A40"
65 HOLE=1:SC=0
66 SCREEN 4:PALETTE 0,8:PALETTE 1,9:PALETTE 2,2:PALETTE 3,15:COLOR 3,8:CLS
67 LOCATE 1,16:PRINT "WORLD GOLF II"
68 COLOR 1:LOCATE 5,5:PRINT "SELECT SKILL LEVEL:":COLOR 2:LOCATE 8,15:PRINT "[1] NOVICE":LOCATE 9,15:PRINT "[2] SKILLED":LOCATE 10,15:PRINT "[3] PRO."
69 A$=INKEY$:IF A$="" THEN 69 ELSE IF A$=CHR$(27) THEN SCREEN 3:COLOR ,8:GOTO 40
70 COLOR 3
71 IF A$="1" THEN LOCATE 8,15:PRINT "[1] NOVICE":FOR V=15 TO 0 STEP -1:SOUND 900,1,V,0:SOUND 800,1,V,1:NEXT:GOTO 75 ELSE IF A$=CHR$(27) THEN SCREEN 2:SCREEN 0:PRINT "Tandy DOS V2.2":NEW
72 IF A$="2" THEN LOCATE 9,15:PRINT "[2] SKILLED":FOR V=15 TO 0 STEP -1:SOUND 900,1,V,0:SOUND 800,1,V,1:NEXT:GOTO 75
73 IF A$="3" THEN LOCATE 10,15:PRINT "[3] PRO.":FOR V=15 TO 0 STEP -1:SOUND 900,1,V,0:SOUND 800,1,V,1:NEXT:GOTO 75
74 GOTO 69
75 SCREEN 3:COLOR 7,8:MD=1
76 SK=VAL(A$):GOTO 500
77 CLS:LINE(50,191)-(70,199),4,BF:LINE(71,191)-(90,199),12,BF:LINE(91,191)-(110,199),13,BF:LINE(111,191)-(130,199),14,BF:LINE(131,191)-(140,199),15,BF
78 LINE(10,196)-(30,196),7:LINE(20,193)-(20,198),15:PSET(8,196),1:PSET(32,196),1
80 S=0:SHT=0:PG=0
84 GOTO 100
85 LINE(0,10)-(159,80),9,BF:LINE(0,81)-(159,170),2,BF:B=4:FOR A=1 TO 8:LINE(0,77+B)-(159,77+B),10,,A*99:LINE(-20,77+B)-(169,77+B),10,,A*99:B=B+B/2:NEXT
86 LINE(0,81)-(159+RND*20,81),9,,RND*30000
87 IF MD=1 THEN CIRCLE(120,40),13,14:PAINT(120,40),14 ELSE IF MD=2 THEN CIRCLE(40,30),13,14:PAINT(40,30),14
88 FOR A=1 TO 2+RND*5
89 C1=-10+RND*169:C2=15+RND*50:LINE(C1,C2)-(C1+20,C2),11:LINE(C1-5,C2-1)-(C1+14,C2-1),11:LINE(C1-2,C2-2)-(C1+9,C2-2),3:LINE(C1+9,C2+1)-(C1+17,C2+1),3
90 NEXT:PALETTE 8,8
91 SZ=100-DIS:IF DIS<0 THEN DIS=0
92 VIEW SCREEN(0,10)-(159,170)
95 IF BIRDS=1 THEN FOR A=1 TO 2+RND*20:X=-5+RND*160:S=RND*3:Y=20+RND*45:LINE(X,Y)-(X+S,Y-S*2),1:LINE(X+S,Y-S*2)-(X+S*2,Y),1:LINE(X+S*2,Y)-(X+S*3,Y-S*2),1:LINE(X+S*4,Y)-(X+S*3,Y-S*2),1:NEXT
96 IF MD=3 THEN FOR A=1 TO 30:R=RND*1:X=RND*159:Y=20+RND*50:CIRCLE(X,Y),R,5:PAINT(X,Y),5:NEXT
98 COLOR 11:LOCATE 23,16:PRINT "HL"HOLE:COLOR 7
99 IF DIS<85 THEN 150 ELSE 200
100 REM *HOLE DATA*
101 BIRDS=0:TREE1=0:TREE2=0:TREE3=0:TREE4=0:HILL1=0:HILL2=0:HILL3=0:CITY=0:FENCE=0
105 IF HOLE=1 THEN DIS=80:TREE1=1:HILL1=1:PX=100
110 IF HOLE=2 THEN DIS=99:TREE2=1:HILL1=1:PX=50
115 IF HOLE=3 THEN DIS=42:TREE1=1:TREE3=1:HILL2=1:PX=70
116 IF HOLE=4 THEN DIS=56:TREE3=1:HILL2=1:BIRDS=1:PX=64
117 IF HOLE=5 THEN DIS=78:TREE2=1:BIRDS=1:PX=120
118 IF HOLE=6 THEN DIS=85:TREE4=1:BIRDS=1:FENCE=1:PX=105
119 IF HOLE=7 THEN DIS=120:TREE3=1:TREE4=1:FENCE=1:HILL2=1:PX=40
120 IF HOLE=8 THEN DIS=90:TREE4=1:TREE1=1:HILL2=1:PX=94
121 IF HOLE=9 THEN DIS=64:TREE2=1:TREE3=1:CITY=1:PX=108
122 IF HOLE=10 THEN DIS=20:PX=45
145 GOTO 85
146 IF TREE1=1 THEN 155 ELSE 157
149 .5
150 IF CITY=1 THEN FOR A=1 TO 22:S=SZ:X=-10+RND*179:X1=RND*S/4:Y1=80-RND*S:LINE(X+X1,81)-(X,Y1),7,BF:LINE(X1+X,81)-(X+X1,Y1),8:LINE(X+X1,Y1)-(X,Y1),8:LINE(X,Y1)-(X,81),8:NEXT
151 IF HILL1=1 THEN VIEW SCREEN(0,81)-(159,10):Y=78-SZ/2:X=3:LINE(-1,81)-(-1,Y),4:FOR A=0 TO 170 STEP 10:Y2=Y+(4-RND*9):LINE(X,Y)-(A,Y2),4:X=A:Y=Y2:NEXT:LINE(160,81)-(160,0),4:PAINT(20,80),4:PAINT(90,80),4:VIEW SCREEN(0,10)-(159,170)
152 IF HILL2=1 THEN Y=79-RND*SZ/2:X=0:FOR A=0 TO 160+SZ STEP SZ/4:Y2=79-RND*SZ/2:LINE(X,Y)-(A,Y2),8:X=A:Y=Y2:NEXT:LINE(-1,81)-(-1,0),8:LINE(160,81)-(160,0),8:LINE(0,81)-(159,81),8:PAINT(80,80),8
153 IF HILL3=1 THEN Y=79-RND*SZ:X=0:FOR A=0 TO 160+SZ STEP SZ/9:Y2=79-RND*SZ:LINE(X,Y)-(A,Y2),3:X=A:Y=Y2:NEXT:LINE(-1,81)-(-1,0),3:LINE(160,81)-(160,0),3:LINE(0,81)-(159,81),3:PAINT(80,80),3
154 IF FENCE=1 THEN Y1=80+SZ/8:Y2=80+SZ/18:Y3=80-SZ/28:LINE(0,Y2)-(159,Y2-SZ/29),5,BF:FOR A=-10 TO 165 STEP SZ/8:LINE(A,Y1)-(A+SZ/39,Y2),15,BF:LINE(A,Y2)-(A+SZ/39,Y3),7,BF:NEXT:GOTO 146 ELSE 146
155 FOR A=-RND*20 TO 160+RND*10 STEP SZ/5+(RND*(SZ/2))*2:A=A-RND*(SZ/9):SS=SZ+RND*SZ:Y=80+SS/5:Y2=80-SS/3:ST=SS/3:LINE(A-(ST/29),Y)-(A+(ST/29),Y-ST/2),4,BF:LINE(A-ST/3,Y-ST/2)-(A+ST/3,Y-ST/2),14:LINE(A+ST/3,Y-ST/2)-(A,Y2),14
156 LINE(A,Y2)-(A-ST/3,Y-ST/2),14:PAINT(A,(Y-ST/2)-1),14:LINE(A-ST/3,Y-ST/2)-(A+ST/3,Y-ST/2),15:LINE(A+ST/3,Y-ST/2)-(A,Y2),15:NEXT
157 IF TREE2=0 THEN 160
158 FOR A=-RND*20 TO 160+RND*10 STEP SZ/3+(RND*(SZ/2))*2:A=A+(SZ/8-RND*(SZ/4)):SS=SZ+RND*SZ+RND*15:Y=80+SS/4:Y2=80-SS/2:ST=SS/4:LINE(A-(ST/39),Y)-(A+(ST/39),Y-ST/2),8,BF:LINE(A-ST/3,Y-ST/2)-(A+ST/3,Y-ST/2),12:LINE(A+ST/3,Y-ST/2)-(A,Y2),12
159 LINE(A,Y2)-(A-ST/3,Y-ST/2),12:PAINT(A,(Y-ST/2)-1),12:LINE(A-ST/3,Y-ST/2)-(A+ST/3,Y-ST/2),4:LINE(A+ST/3,Y-ST/2)-(A,Y2),4:NEXT
160 IF TREE3=0 THEN 162
161 FOR A=-RND*20 TO 160+RND*10 STEP SZ/3+(RND*(SZ/2))*2:A=A+(SZ/8-RND*(SZ/4)):SS=SZ+RND*SZ+RND*17:Y=80+SS/3:Y2=80-SS/2:ST=SS/4:LINE(A-(ST/33),Y)-(A+(ST/33),Y2),8,BF:CIRCLE(A,Y2),SS/3,13:PAINT(A,Y2+SS/6),13:CIRCLE(A,Y2),SS/3,5:NEXT
162 IF TREE4=0 THEN 165
163 FOR A=-RND*20 TO 160+RND*10 STEP SZ/2+(RND*(SZ/2))*2:A=A+(SZ/8-RND*(SZ/4)):SS=SZ+RND*SZ+RND*17:Y=80+SS/3:Y2=80-SS/2:ST=SS/4:LINE(A-(ST/33),Y)-(A+(ST/33),Y2),3,BF:CIRCLE(A,Y2),SS/4,10:PAINT(A,Y2+SS/6),10:CIRCLE(A,Y2),SS/3,14:CIRCLE(A,Y2+SS/4),SS/8,14
164 PAINT(A,Y2+SS/4),14:NEXT
165 REM
195 PY=80+(SZ/2):P2=80-(SZ/5):X2=PX-(SZ/10):LINE(PX,PY)-(PX,P2),15:LINE(PX,P2)-(X2,P2+(SZ/8)),11:LINE(X2,P2+(SZ/8))-(PX,P2+(SZ/5)),11:LINE(PX,P2+(SZ/5))-(PX,P2),11:PAINT(PX-1,P2+(SZ/9)),11
196 LINE(PX,P2)-(X2,P2+(SZ/8)),8:LINE(X2,P2+(SZ/8))-(PX,P2+(SZ/5)),8:LINE(PX,P2+(SZ/5))-(PX,P2),8:IF DIS<13 THEN LINE(PX+1,PY)-(PX+1,P2),7
197 IF HOLE<4 THEN PG=0 ELSE IF HOLE>4 AND HOLE<9 THEN PG=1 ELSE IF HOLE>9 AND HOLE<13 THEN PG=2 ELSE PG=3
199 IF SHT=0 THEN LINE(50,170)-(50,160),4:CIRCLE(50,160),6,1:PAINT(50,160),1:CIRCLE(50,160),6,9:LINE(110,170)-(110,160),4:CIRCLE(110,160),6,1:PAINT(110,160),1:CIRCLE(110,160),6,9
200 LINE(10,196)-(30,196),7
201 VIEW SCREEN(0,0)-(159,199):COLOR 7,8
202 CL=1:GOSUB 203:GOTO 205
203 IF CL=1 THEN C$="Driver" ELSE IF CL=2 THEN C$="4 Iron" ELSE IF CL=3 THEN C$="8 Iron" ELSE IF CL=4 THEN C$="Putter"
204 PLAY "a40":LOCATE 23,2:PRINT C$:RETURN
205 LINE(0,0)-(159,9),0,BF:LINE(50,185)-(149,190),0,BF
206 WD=4-FIX(RND*8):PSET(150,1),2:PSET(150,5),2:LINE(150,3)-(150+(WD*2),3),12
210 LOCATE 1,1:PRINT "Distance"FIX(DIS/2)"m."
215 XX=75
217 IF SHT>(5-SK-(PG/2)) THEN 350
220 PUT(XX,150),A
225 IF STICK(0)<10 AND XX>10 THEN PUT(XX,150),A:XX=XX-2:GOTO 220 ELSE IF STICK(0)>99 AND XX<140 THEN PUT(XX,150),A:XX=XX+2:GOTO 220
226 IF STRIG(1)=-1 THEN 230
227 IF INKEY$=CHR$(27) THEN CLS:GOTO 350
228 IF STICK(1)<10 AND CL<4 THEN CL=CL+1:GOSUB 203 ELSE IF STICK(1)>99 AND CL>1 THEN CL=CL-1:GOSUB 203
229 GOTO 225
230 PW=0:PUT(XX,150),A:FOR V=15 TO 2 STEP -1:SOUND 999,1,V,0:SOUND 899,1,V,1:NEXT:FOR P=1 TO 300:NEXT:XX=XX+4
231 LINE(PW+50,185)-(PW+50,190),0:IF O=0 THEN PW=PW+1 ELSE PW=PW-1
232 IF PW<1 THEN O=0 ELSE IF PW>90 THEN O=1
233 LINE(PW+50,185)-(PW+50,190),3:IF STRIG(1)=-1 THEN 235
234 GOTO 231
235 LINE(PW+50,185)-(PW+50,190),0:PLAY "A40C40"
236 SN=11:WHILE STRIG(1)=-1:WEND
237 SN=SN-1:IF SN<-10 THEN SN=-10:GOTO 245
238 PSET(21+SN,196),7:PSET(20+SN,196),14
239 IF STRIG(1)=-1 THEN 245
240 FOR P=0 TO 70-(SK*34):NEXT
241 IF SN=10 THEN PSET(31,196),0
244 GOTO 237
245 LINE(80,160)-(80,158),4:LINE(79,155)-(81,157),12,BF:PSET(80,155),2:LOCATE 1,1:PRINT "              ":IF CL=1 THEN TOP=2.5 ELSE IF CL=2 THEN TOP=2.3 ELSE IF CL=3 THEN TOP=2.2 ELSE TOP=1.8
246 BX=77:BY=150:PUT(BX,BY),B1:AA=1.2:XP=72:YP=105:S1=30:S2=50:O=0:SP=.2
247 GET(45,102)-(89,155),B:DR=XX+(SN*(SK-1)):DY=170-((PW*(5-CL))/2):IF SK=3 THEN DR=DR+(WD*((PW/8)*CL))
248 SX=XP+S1*COS(AA):SY=YP+S2*SIN(AA):PUT(45,102),B,PSET
249 LINE(XP,YP)-(SX,SY),15:LINE(SX,SY)-(SX+3,SY-2),11
250 IF O=0 THEN AA=AA+.2 ELSE AA=AA-SP:SP=SP+.05
251 IF AA>TOP THEN O=1 ELSE IF AA<1.3 AND SP>.2 THEN PUT(45,102),B,PSET:PUT(BX,BY),B1:BX=BX+2:BY=BY-2:PUT(BX,BY),B1:GET(45,102)-(89,155),B:NOISE 4,15,1:IF AA<1.1 THEN 255
254 GOTO 248
255 W=0:O=0
256 IF W<10 THEN PUT(BX,BY),B1 ELSE IF W>10 AND W<20 THEN PUT(BX,BY),B2 ELSE PUT(BX,BY),B3
257 IF BX>DR THEN BX=BX-2 ELSE BX=BX+2
258 IF BY>DY AND O=0 AND BY>25 THEN BY=BY-2 ELSE BY=BY+2:O=1:IF BY>BY/6 THEN 270
259 W=W+1
260 IF W<10 THEN PUT(BX,BY),B1 ELSE IF W>10 AND W<20 THEN PUT(BX,BY),B2 ELSE PUT(BX,BY),B3
265 GOTO 256
270 REM *TAG*
274 PX2=PX
275 MV=(PX-(XX)):PX=PX+(MV/2)
280 DM=((PW*(5-CL))/3):XM=XX+(SN*(SK-1)):IF SK=3 THEN XM=XM+(WD*(PD+1))
281 DS2=DIS-DM:SHT=SHT+1:IF DS2<-2 THEN 300
282 DIS=DS2:IF DIS<2 AND DIS>-1 AND XM<PX+2 AND XM>PX-2 THEN 400 ELSE IF SHT>(5-SK-(PG/2)) THEN 350
283 IF DIS<2 THEN DIS=2
284 FOR V=15 TO 3 STEP -1:SOUND 999,1,V,0:SOUND 800,1,V,1:SOUND 850,1,V,2:NEXT:GOTO 85
300 LINE(40,50)-(120,70),15,B:LINE(41,51)-(119,69),7,B:LINE(42,52)-(118,68),8,BF
305 COLOR 15:LOCATE 8,7:PRINT "OVER DST"
310 FOR V=15 TO 0 STEP -1:SOUND V*15,1,V,0:SOUND 900,1,V,1:NEXT
315 FOR P=0 TO 400:NEXT
316 PX=PX2
317 IF SHT>(5-SK-(PG/2)) THEN 350
320 GOTO 85
350 SCREEN 2:SCREEN 3:COLOR 7,8
351 FOR V=15 TO 3 STEP -1:SOUND 1200,1,V,0:SOUND 1250,1,V,1:NEXT
355 LINE(0,10)-(159,39),1,BF:LINE(0,40)-(159,69),9,BF:LINE(0,70)-(159,100),10,BF:LINE(0,101)-(159,170),2,BF
356 FOR A=1 TO 6
357 C1=-10+RND*169:C2=12+RND*28:LINE(C1,C2)-(C1+20,C2),11:LINE(C1-5,C2-1)-(C1+14,C2-1),11:LINE(C1-2,C2-2)-(C1+9,C2-2),3:LINE(C1+9,C2+1)-(C1+17,C2+1),3
358 NEXT
360 LINE(40,140)-(44,90),15,B:LINE(115,140)-(119,90),15,B:LINE(41,140)-(43,90),7,BF:LINE(116,140)-(118,90),7,BF:LINE(35,51)-(125,90),15,B:LINE(36,52)-(124,89),8,BF
365 COLOR 7,8:LOCATE 8,7:PRINT "SCORE 1UP":COLOR 3
366 LOCATE 10,9:S$=RIGHT$(STR$(SC),LEN(STR$(S))-1):SS$=STRING$(4-LEN(S$),"0")+S$:PRINT SS$
370 GET(1,10)-(159,40),B:GET(0,10)-(0,40),C:PUT(159,10),C,PSET:PUT(0,10),B,PSET
371 IF STRIG(1)=-1 THEN PLAY "a40":GOTO 40
375 GOTO 370
400 LINE(30,50)-(130,70),15,B:LINE(31,51)-(129,69),7,B:LINE(32,52)-(128,68),8,BF
405 COLOR 15:LOCATE 8,6:PRINT "NEXT HOLE!"
406 SC=SC+(6-SHT)*HOLE
410 FOR V=15 TO 3 STEP -1:SOUND RND*999,1,V,0:SOUND RND*999,1,V,1:NOISE 4,V,1:NEXT
411 SHT=0:HOLE=HOLE+1:IF HOLE>18 THEN HOLE=1
415 GOTO 100
500 CLS:COLOR 7,8
501 FOR V=15 TO 2 STEP -1:SOUND 500,1,V,0:SOUND RND*999,1,V,1:NOISE 5,V,1:NEXT
505 LOCATE 1,5:PRINT "TIME OF DAY"
506 LINE(0,8)-(159,8),7
510 LINE(10,30)-(100,170),15,B:LINE(11,31)-(99,169),7,B:PALETTE 8,0
511 COLOR 8:LOCATE 8,3:PRINT "MORNING":LOCATE 12,3:PRINT "MID-DAY":LOCATE 16,3:PRINT "EVENING"
515 O=1:COLOR 14
520 IF O=1 THEN LOCATE 8,10:PRINT CHR$(17) ELSE IF O=2 THEN LOCATE 12,10:PRINT CHR$(17) ELSE IF O=3 THEN LOCATE 16,10:PRINT CHR$(17)
524 FOR P=1 TO 150:NEXT
525 IF STICK(0)<10 AND O>1 THEN PLAY "C40":GOTO 540 ELSE IF STICK(0)>109 AND O<3 THEN PLAY "C40":GOTO 545
530 IF STRIG(1)=-1 THEN 550
535 GOTO 525
540 IF O=1 THEN LOCATE 8,10:PRINT CHR$(32) ELSE IF O=2 THEN LOCATE 12,10:PRINT CHR$(32) ELSE IF O=3 THEN LOCATE 16,10:PRINT CHR$(32)
541 O=O-1:GOTO 520
545 IF O=1 THEN LOCATE 8,10:PRINT CHR$(32) ELSE IF O=2 THEN LOCATE 12,10:PRINT CHR$(32) ELSE IF O=3 THEN LOCATE 16,10:PRINT CHR$(32)
546 O=O+1:GOTO 520
550 IF O=1 THEN RESTORE 560:FOR I=1 TO 15:READ I2:PALETTE I,I2:NEXT:MD=2:GOTO 77 ELSE IF O=3 THEN RESTORE 570:FOR I=1 TO 15:READ I2:PALETTE I,I2:NEXT:MD=3:GOTO 77
555 GOTO 77
560 DATA 1,10,3,5,4,5,7,8,12,14,2,12,13,13,15
570 DATA 9,2,3,5,11,5,7,8,1,8,14,12,13,14,7
900 SCREEN 2:PALETTE 1,2
901 CLS:S=50
905 A=A+.1
910 Y=100+(S*COS(A)):PSET(X,Y):X=X+1:IF X>639 THEN CLS:X=0
915 GOTO 905
