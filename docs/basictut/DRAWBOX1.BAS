10 DIM CORNER(4)
20 KEY OFF
30 W = 15 ' width of box
40 H = 6 ' height of box
50 DATA 205, 186, 201, 187, 188, 200
60 READ HORIZ, VERTICAL
70 FOR I = 1 TO 4
80 READ CORNER(I)
90 NEXT
100 X = 0
110 WHILE X < 1 OR X > 25 - H
120 INPUT "Upper left row";X
130 WEND
140 Y = 0
150 WHILE Y < 1 OR Y > 80 - W
160 INPUT "Upper left column";Y
170 WEND
180 COLOR 15,1
190 CLS
200 LOCATE X,Y
210 PRINT CHR$(CORNER(1));      ' top line
220 FOR I = 1 TO W-2 : PRINT CHR$(HORIZ); : NEXT
230 PRINT CHR$(CORNER(2));
240 FOR J = 1 TO H - 2    ' create middle section
250 LOCATE X+J,Y
260 PRINT CHR$(VERTICAL);
270 FOR I = 1 TO W-2 : PRINT CHR$(32); : NEXT
280 PRINT CHR$(VERTICAL);
290 NEXT
300 LOCATE X+H-1, Y
310 PRINT CHR$(CORNER(4));      ' bottom line
320 FOR I = 1 TO W-2 : PRINT CHR$(HORIZ); : NEXT
330 PRINT CHR$(CORNER(3));
