9000 PRINT "Vfind? ";:DEF SEG:B=PEEK(47)*256+PEEK(46)
9010 DEFSTR M-Z:DEFINT D-L:H=VARPTR(#1)+51:POKE H,6
9020 FOR I=H+1 TO H+252:POKE I,1:NEXT:POKE I,0:POKE I+1,0
9030 POKE I+2,0:FOR I=H+11 TO H+14:POKE I,2:NEXT
9040 POKE I,0:POKE H+132,6:POKE H+143,6:POKE H+28,2
9050 POKE H+29,4:POKE H+31,8:POKE H+34,5:POKE H+209,9
9060 FOR I=65 TO 90:POKE H+I,3:NEXT
9070 N="ABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789$#%!."
9080 Z=SPACE$(255):B.=B:B=PEEK(49)*256+PEEK(48):C=0:A=B:F=0
9090 LINE INPUT Q:IF Q>"" THEN PRINT ELSE END
9100 FOR I=1 TO LEN(Q):IF ASC(MID$(Q,I))<97 THEN 9120
9110 MID$(Q,I)=CHR$(ASC(MID$(Q,I))-32)
9120 NEXT:M="######"+STRING$(6,29)
9130 A=PEEK(B+1)*256+PEEK(B):B=B+3:C=PEEK(B)*256+PEEK(B-1)
9140 IF C<B. THEN PRINT USING M;C; ELSE PRINT SPC(6):END
9150 B=B+1
9160 F=PEEK(H+PEEK(B))
9170 ON F GOTO 9150,9190,9230,9190,9200,9210,,9190,9220
9180 B=B+2:GOTO 9160
9190 B=B+PEEK(H+PEEK(B))+1:GOTO 9160
9200 B=B+1:IF PEEK(B)=34 THEN 9150 ELSE IF PEEK(B) THEN 9200
9210 B=A:GOTO 9130
9220 I=3:LSET Z="FN":B=B+1:GOTO 9240
9230 I=2:LSET Z=CHR$(PEEK(B)):B=B+1
9240 WHILE INSTR(N,CHR$(PEEK(B))):MID$(Z,I)=CHR$(PEEK(B)):B=B+1
9250 I=I+1:WEND:IF PEEK(B)=40 THEN MID$(Z,I)="(":B=B+1:I=I+1
9260 IF LEFT$(Z,I-1)<>Q THEN 9160
9270 PRINT STRING$(6,28);:B=A:GOTO 9130
